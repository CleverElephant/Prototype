
plugins {
    id "java-library"
    id "pl.allegro.tech.build.axion-release" version "1.14.4"
    id "com.github.hierynomus.license" version "0.16.1"
    id "maven-publish"
    id "io.github.themrmilchmann.ecj" version "0.1.0"
}

allprojects {
    group = "com.github.cleverelephant.prototype"
    version = scmVersion.version
    
    apply plugin: "java-library"
    apply plugin: "pl.allegro.tech.build.axion-release"
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "maven-publish"
    apply plugin: "io.github.themrmilchmann.ecj"

    repositories {
        mavenCentral()
    }


    java {
        sourceCompatibility = "18"
        targetCompatibility = "18"
    }
    
    license {
        header = rootProject.file("gradle/header.txt")
        exclude "**/*.json"
        
        ext {
            year = Calendar.getInstance().get(Calendar.YEAR)
            name = "Benjamin Wied"
            email = "88872078+CleverElephant@users.noreply.github.com"
        }
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = "sources"
    }

    publishing {
        publications {
            prototype(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
    
        repositories {
            mavenLocal()
        }
    }
}